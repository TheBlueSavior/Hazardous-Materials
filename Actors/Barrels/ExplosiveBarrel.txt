Class HM_ExpBarrel : Actor replaces ExplosiveBarrel
{
	int timer;
	Default
	{
		Health 40;
		Radius 10;
		Height 32;
		+SOLID
		+SHOOTABLE
		+ACTIVATEMCROSS
		+DONTGIB
		+NOICEDEATH
		+OLDRADIUSDMG
		+NOBLOODDECALS
		DeathSound "";
		Bloodtype "HM_NormalHitSpark";
		Obituary "$OB_BARREL";
	}
	
	override void PostBeginPlay()
	{
	Super.PostBeginPlay();
	tex[0] = TexMan.CheckForTexture("DUSTC0",TexMan.Type_Sprite);
	tex[1] = TexMan.CheckForTexture("GNSML0",TexMan.Type_Sprite);
	tex[2] = TexMan.CheckForTexture("LENYA0",TexMan.Type_Sprite);
	}
 
	TextureID tex[3];
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay { timer = random(15,75); }
	SpawnLoop:
		BRL_ A 6;
		BRL_ A 6 A_JumpIfHealthLower(25,"Burning");
		Loop;
	Burning:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("Fire/Idle",32);
	BurningLoop:
		BRL_ A 2 Light("HML_BarrelIgnited")
		{
		A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 4,size: 90.0,xoff: random(-10,10),yoff:random(-10,10),zoff:random(15,25),startalphaf: 0.6);
		A_SpawnItemEx("HM_BarrelFire",random(-5,5),random(-5,5),random(5,30),frandom(-1,1),frandom(-1,1),frandom(1,2),0,SXF_NOCHECKPOSITION);
		timer--;
		}
		BRL_ A 0
		{
			if(timer < 0)
			SetStateLabel("Boom");
		}
		Loop;
	Boom:
		TNT1 A 0;	
		TNT1 A 0 A_Die;
		Goto Death;
	Death:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("HMBarrel/Strain",33,CHANF_OVERLAP,frandom(0.7,1.0));
		BRL_ AAAAAAAA 1 Light("HML_BarrelIgnited")
		{
		A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 4,size: 100.0,xoff: random(-10,10),yoff:random(-10,10),zoff:random(15,25),startalphaf: 0.6);
		}
		BRL_ BBBBBBBBBBB 1 Light("HML_BarrelIgnited")
		{
		A_SpawnParticleEx("",tex[2],style: STYLE_Add,flags: SPF_RELATIVE|SPF_FULLBRIGHT,lifetime: 4,size: 135.0,xoff: random(-10,10),yoff:random(-10,10),zoff:random(15,25),startalphaf: 0.6);
		}
		TNT1 A 0 A_StopSound(32);
		BRK_ B 1 Light("HML_BarrelExplode")
		{
		A_Scream();
		A_StartSound("HMBarrel/Explode",34,CHANF_OVERLAP);
		A_StartSound("HMBarrel/ExpExtra",35,CHANF_OVERLAP,frandom(0.85,1.00));
		A_StartSound("HMBarrel/Thud",36,CHANF_OVERLAP);
		A_StartSound("HMBarrel/Distant",37,CHANF_OVERLAP,frandom(0.8,0.95));
		A_StartSound("HMBarrel/Break",38,CHANF_OVERLAP,frandom(0.3,0.8));
		A_SpawnParticleEx("",TexMan.CheckforTexture("SPKOA0"),style: STYLE_Add,flags: SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL,lifetime:22,size:750,zoff:25,startalphaf:0.95
	   ,fadestepf: -1,sizestep: -20,startroll: random(-180,180));
		A_QuakeEx(2, 2, 2, 60, 0, 500, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 800);
		A_SpawnParticleEx( "", tex[1], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL, lifetime: random(30,250),size: frandom(200,300), xoff: 0, zoff: 20, velx: frandom(-0.4,0.4), vely: frandom(-0.4,0.4), velz: frandom(-0.2,0.5),startalphaf: frandom(0.7,1)
		,fadestepf: -1, sizestep: frandom(0.0,5), startroll: random(-180,180), rollvel: frandom(-0.2,0.2));
		}
		TNT1 AAAAAAAAAA 0
		{
			A_SpawnParticleEx
			( "727272", tex[0], style: STYLE_Normal, flags: SPF_RELATIVE|SPF_ROLL,
			lifetime: random(40,70), size: frandom(2,25), zoff: random(10,35),
			velx: frandom(-20,20), vely: frandom(-20,20), velz: frandom(1,25),
			accelz: frandom(-0.3,-1.2), startalphaf: 1.0, sizestep: -0.2, startroll: random(-180,180), rollvel: random(-4,4) );
		}
		TNT1 A 0 A_Explode(300,250);
		TNT1 AAA 0 A_SpawnItemEx("HM_BarrelShockwave",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION,150);
		TNT1 AAAAA 0 A_SpawnItemEx("HM_BarrelShrapnel", frandom(-20, 20), frandom(-20, 20), frandom(5, 56), (vel.x / 4) + frandom(-20, 20), (vel.y / 4) + frandom(-20, 20), (vel.z / 4) + frandom(1, 20), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY, 130);
		TNT1 A 0 A_SpawnItemEx("HM_BarrelSprayExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		TNT1 AAAAAA 0 A_SpawnItemEx("HM_GenericExplosionFlames",0,0,5,random(-5,5),random(-5,5),random(0,5),0,SXF_NOCHECKPOSITION);
		TNT1 A 20 Light("HML_BarrelExplode");
		TNT1 A 1050 A_BarrelDestroy;
		TNT1 A 5 A_Respawn;
		Stop;
	}
}